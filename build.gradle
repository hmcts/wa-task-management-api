plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '3.0.6'
  id 'org.owasp.dependencycheck' version '8.2.1'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'org.sonarqube' version '4.0.0.2929'
  id 'uk.gov.hmcts.java' version '0.12.43'
  id 'info.solidsoft.pitest' version '1.9.11'
  id 'io.freefair.lombok' version '8.0.1'
  // If Pact version is changed, make sure serenity report works.
  id 'au.com.dius.pact' version '4.3.12'
  id 'net.serenity-bdd.serenity-gradle-plugin' version '3.2.0'
  id 'org.flywaydb.flyway' version '9.19.1'
  id 'jvm-test-suite'
}

apply plugin: "info.solidsoft.pitest"

group = 'uk.gov.hmcts.reform'
version = '1.0.2'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

sourceSets {

  testUtils {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/testUtils/java')
    }
    resources.srcDir file('src/testUtils/resources')
  }

  functionalTest {
    java {
      compileClasspath += testUtils.output
      runtimeClasspath += testUtils.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += testUtils.output
      runtimeClasspath += testUtils.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }

}
tasks.withType(Copy).configureEach {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
    showStandardStreams = true
  }
}

test {
  failFast = true
}

tasks.register('functional', Test) {
  maxParallelForks = (System.getenv('MAX_FT_TESTS_PARALLEL_FORKS') ? System.getenv('MAX_FT_TESTS_PARALLEL_FORKS') : 1) as int
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath

}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

project.ext {
  pacticipant = 'wa_task_management_api'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.9.3'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.40.0"
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      excludes = ["com.gargoylesoftware.htmlunit.javascript.host.css.StyleAttributes"]
    }
  }
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  pitestVersion.set('1.7.4')
  junit5PluginVersion.set('0.15')
  targetClasses = ['uk.gov.hmcts.reform.wataskmanagementapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.wataskmanagementapi.taskconfiguration.config.*',
    'uk.gov.hmcts.reform.wataskmanagementapi.config.*',
    'uk.gov.hmcts.reform.wataskmanagementapi.Application'
  ]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 50
  timeoutConstInMillis = 30000
  mutators.set([
    'CONDITIONALS_BOUNDARY',
    'INCREMENTS',
    'INVERT_NEGS',
    'MATH',
    'NEGATE_CONDITIONALS',
    'VOID_METHOD_CALLS',
    'EMPTY_RETURNS',
    'FALSE_RETURNS',
    'NULL_RETURNS',
    'PRIMITIVE_RETURNS',
    'TRUE_RETURNS',
    'REMOVE_CONDITIONALS'
  ])
}

tasks.register('tests') {
  description = "Runs junit , integration and functional tests"
  dependsOn test, integration, functional
}

project.tasks['pitest'].group = "Verification"

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport


// Ensures that functional tests index page is generated
// Functional tests should run two tasks "functional" and "aggregate"
gradle.startParameter.continueOnFailure = true
functional.finalizedBy aggregate

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: wa-task-management-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:wa-task-management-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/config/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/Application.java," +
      "src/testUtils/java/uk/gov/hmcts/reform/wataskmanagementapi/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/clients/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/domain/entities/search/parameter/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/cft/replicarepository/**"
    property "sonar.cpd.exclusions", "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/services/calendar/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'

  //CVE Scanning only relevant to production code that is published, not test or other implementations
  scanConfigurations += 'releaseCompileClasspath'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'commons-io', name: 'commons-io', version: '2.8.0'

    dependencySet( group: 'org.springframework.security', version: '5.7.8'){
      entry 'spring-security-core'
      entry 'spring-security-acl'
      entry 'spring-security-aspects'
      entry 'spring-security-cas'
      entry 'spring-security-config'
      entry 'spring-security-crypto'
      entry 'spring-security-data'
      entry 'spring-security-ldap'
      entry 'spring-security-messaging'
      entry 'spring-security-oauth2-client'
      entry 'spring-security-oauth2-core'
      entry 'spring-security-oauth2-jose'
      entry 'spring-security-oauth2-resource-server'
      entry 'spring-security-openid'
      entry 'spring-security-remoting'
      entry 'spring-security-rsocket'
      entry 'spring-security-saml2-service-provider'
      entry 'spring-security-taglibs'
      entry 'spring-security-test'
      entry 'spring-security-web'
    }

    dependencySet(group: 'org.springframework.cloud', version: '4.0.2') {
      entry 'spring-cloud-starter-openfeign'
      entry 'spring-cloud-openfeign-core'
    }

    //dependencySet(group: 'org.seleniumhq.selenium', version: '4.2.2') {
     // entry 'selenium-java'
    //  entry 'selenium-remote-driver'
     // entry 'selenium-firefox-driver'
     // entry 'selenium-chrome-driver'
     // entry 'selenium-safari-driver'
     // entry 'selenium-edge-driver'
     // entry 'selenium-api'
    //  entry 'selenium-support'
    //}

    dependencySet(group: 'org.yaml', version: '2.0') {
      entry 'snakeyaml'
    }

  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

def versions = [
  commonsIo           : '2.7',
  junit               : '5.9.0',
  junitPlatform       : '1.9.2',
  lombok              : '1.18.22',
  reformLogging       : '6.0.1',
  restAssured         : '5.3.0',
  springDoc           : '1.7.0',
  serenity            : '3.7.1',
  gradlePitest        : '1.9.11',
  pitest              : '1.9.11',
  pactVersion         : '4.3.4',
  logbook             : '3.0.0',
  sonarPitest         : '0.5',
  springBoot          : springBoot.class.package.implementationVersion,
  springFramework     : '6.0.8',
  springSecurity      : '6.0.3',
  testcontainers      : '1.17.6',
  wiremock            : '2.35.0',
  flywaydb            : '9.19.1',
]

ext['jackson.version'] = '2.14.1'


dependencies {


  implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
  implementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured


  //implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
  //implementation group: 'jakarta.persistence', name: 'jakarta.persistence-api', version: '3.1.0'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot
  implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: versions.springSecurity
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client', version: '2.7.11'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.6'

  implementation('org.springframework.retry:spring-retry')

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc
  implementation group: 'org.springdoc', name: 'springdoc-openapi-webmvc-core', version: versions.springDoc

  implementation group: 'org.zalando', name: 'logbook-spring-boot-starter', version: versions.logbook
  implementation group: 'org.zalando', name: 'problem-spring-web-starter', version: '0.29.1'


  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.3'
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-web', version: '3.4.12'

  implementation group: 'net.minidev', name: 'json-smart', version: '2.4.10'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '5.10.8'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging

  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation group: 'org.hibernate', name: 'hibernate-core', version: '5.6.15.Final'
  //implementation group: 'org.hibernate', name: 'hibernate-core-jakartha', version: '5.6.15.Final'
  implementation group: 'org.hibernate', name: 'hibernate-validator', version: '7.0.5.Final'

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '3.1.4'
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '4.9.1'

  implementation group: 'com.auth0', name: 'java-jwt', version: '4.4.0'

  implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-paranamer', version: '2.14.2'

  implementation group: 'org.flywaydb', name: 'flyway-core', version: versions.flywaydb
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
  // required to convert postgres to hibernate types
  implementation group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.21.1'

  //cache
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.5'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers
  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers

  testImplementation group: 'org.awaitility', name: 'awaitility', version: '3.0.0'
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8-standalone', version: versions.wiremock
  testImplementation group: 'org.flywaydb', name: 'flyway-core', version: versions.flywaydb
  testImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'


  testImplementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
  testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: '0.5'

  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junit

  testImplementation group: 'com.obsidiandynamics.pojotester', name: 'core', version: '0.9.0'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0'
  testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '31.1-jre'

  //Pact contract testing
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pactVersion
  //contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit', version: versions.pactVersion

  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pactVersion
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pactVersion
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pactVersion

  ///contractTestRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit)
  //contractTestImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit)

  testUtilsImplementation sourceSets.main.runtimeClasspath
  testUtilsImplementation sourceSets.test.runtimeClasspath

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
}

tasks.register('runAndPublishConsumerPactTests', Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'build/pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    version = project.pacticipantVersion
  }
}

tasks.register('contract', Test) {
  description = "Runs the consumer Pact tests"
  group = 'Verification'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

tasks.register('runProviderPactVerification', Test) {
  logger.lifecycle("Runs provider pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
  systemProperty 'pact.provider.version', project.pacticipantVersion
}

//task fortifyScan(type: JavaExec)  {
//  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
//  classpath += sourceSets.test.runtimeClasspath
//  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
//}

runProviderPactVerification.dependsOn contract

runProviderPactVerification.finalizedBy pactVerify

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

mainClassName = 'uk.gov.hmcts.reform.wataskmanagementapi.Application'

bootJar {
  getArchiveFileName().set(provider {
    'wa-task-management-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
