plugins {
  id 'idea'
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.4.4'
  id 'org.owasp.dependencycheck' version '12.1.3'
  id 'com.github.ben-manes.versions' version '0.48.0'
  id 'org.sonarqube' version '5.1.0.4882'
  id 'info.solidsoft.pitest' version '1.15.0'
  id 'io.freefair.lombok' version '8.10.2'
  // If Pact version is changed, make sure serenity report works.
  id 'au.com.dius.pact' version '4.6.17'
  id 'net.serenity-bdd.serenity-gradle-plugin' version '4.2.33'
  id 'org.flywaydb.flyway' version '9.3.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

sourceSets {

  testUtils {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/testUtils/java')
    }
    resources.srcDir file('src/testUtils/resources')
  }

  functionalTest {
    java {
      compileClasspath += testUtils.output
      runtimeClasspath += testUtils.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += testUtils.output
      runtimeClasspath += testUtils.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }

}
tasks.withType(Copy) {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
    showStandardStreams = true
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  maxParallelForks = (System.getenv('MAX_FT_TESTS_PARALLEL_FORKS') ? System.getenv('MAX_FT_TESTS_PARALLEL_FORKS') : 1) as int
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED']
  // finalizedBy aggregate
}

task integration(type: Test) {
  jvmArgs = ['-Xms512m', '-Xmx2048m']
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

build.dependsOn integration

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

project.ext {
  pacticipant = 'wa_task_management_api'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.26.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.40.0"
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      excludes = ["com.gargoylesoftware.htmlunit.javascript.host.css.StyleAttributes"]
    }
  }
}

jacocoTestReport {
  dependsOn test, integration
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  junit5PluginVersion = '1.2.1'
  targetClasses = ['uk.gov.hmcts.reform.wataskmanagementapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.wataskmanagementapi.taskconfiguration.config.*',
    'uk.gov.hmcts.reform.wataskmanagementapi.config.*',
    'uk.gov.hmcts.reform.wataskmanagementapi.Application'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 80
}

task tests {
  description = "Runs junit , integration and functional tests"
  dependsOn test, integration, functional
}

project.tasks['pitest'].group = "Verification"

project.tasks['sonarqube'].dependsOn jacocoTestReport


// Ensures that functional tests index page is generated
// Functional tests should run two tasks "functional" and "aggregate"
gradle.startParameter.continueOnFailure = true

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: wa-task-management-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:wa-task-management-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/config/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/Application.java," +
      "src/testUtils/java/uk/gov/hmcts/reform/wataskmanagementapi/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/clients/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/domain/entities/search/parameter/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/cft/replicarepository/**"
    property "sonar.cpd.exclusions", "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/services/calendar/**," +
      "src/main/java/uk/gov/hmcts/reform/wataskmanagementapi/controllers/TaskActionsController.java,"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'

  //CVE Scanning only relevant to production code that is published, not test or other implementations
  scanConfigurations += 'releaseCompileClasspath'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group == 'org.hibernate.orm' && details.requested.name == 'hibernate-core') {
        details.useVersion '6.6.1.Final'
      }
    }
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'commons-io', name: 'commons-io', version: '2.19.0'

    dependencySet( group: 'org.springframework.security', version: '6.4.4'){
      entry 'spring-security-core'
      entry 'spring-security-acl'
      entry 'spring-security-aspects'
      entry 'spring-security-cas'
      entry 'spring-security-config'
      entry 'spring-security-crypto'
      entry 'spring-security-data'
      entry 'spring-security-ldap'
      entry 'spring-security-messaging'
      entry 'spring-security-oauth2-client'
      entry 'spring-security-oauth2-core'
      entry 'spring-security-oauth2-jose'
      entry 'spring-security-oauth2-resource-server'
      entry 'spring-security-openid'
      entry 'spring-security-remoting'
      entry 'spring-security-rsocket'
      entry 'spring-security-saml2-service-provider'
      entry 'spring-security-taglibs'
      entry 'spring-security-test'
      entry 'spring-security-web'
    }

    dependencySet(group: 'org.seleniumhq.selenium', version: '4.2.2') {
      entry 'selenium-java'
      entry 'selenium-remote-driver'
      entry 'selenium-firefox-driver'
      entry 'selenium-chrome-driver'
      entry 'selenium-safari-driver'
      entry 'selenium-edge-driver'
      entry 'selenium-api'
      entry 'selenium-support'
    }

    dependencySet(group: 'ch.qos.logback', version: '1.5.18') {
      entry 'logback-classic'
      entry 'logback-core'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    name = "AzureArtifacts"
    url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
  }
}

def versions = [
  junit         : '5.13.3',
  junitPlatform : '1.13.3',
  reformLogging : '6.1.9',
  springDoc     : '2.2.0',
  serenity      : '3.9.8',
  gradlePitest  : '1.15.0',
  pitest        : '1.16.3',
  sonarPitest   : '0.5',
  pactVersion   : '4.6.17',
  logbook       : '3.9.0',
  tomcat        : '10.1.28',
  testcontainers: '1.21.3',
  mapStruct     : '1.3.0.Final'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-launcher:${versions.junitPlatform}"
  ]
]

ext['snakeyaml.version'] = '2.0'

dependencies {
  implementation group: 'org.mapstruct', name: 'mapstruct-jdk8', version: versions.mapStruct
  implementation group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct
  annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct

  testImplementation group: 'org.mapstruct', name: 'mapstruct-jdk8', version: versions.mapStruct
  testAnnotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct


  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: '3.4.4'


  implementation (group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client', version: '2.7.18') {
    exclude group: 'net.minidev', module: 'json-smart'
  }
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-context', version: '4.2.1'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.2.1'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.9'

  implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.5'

  implementation group: 'org.springframework.security', name: 'spring-security-rsa', version: '1.1.5'

  implementation('org.springframework.retry:spring-retry')

  implementation group: 'org.zalando', name: 'logbook-core', version: versions.logbook
  implementation group: 'org.zalando', name: 'logbook-spring-boot-starter', version: versions.logbook
  implementation group: 'org.zalando', name: 'logbook-httpclient5', version: versions.logbook
  implementation group: 'org.zalando', name: 'problem-spring-web-starter', version: '0.29.1'

  implementation group: 'net.minidev', name: 'json-smart', version: '2.5.2'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.3.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
  implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging

  implementation group: 'org.hibernate', name: 'hibernate-core', version: '6.6.36.Final'
  implementation group: 'org.hibernate', name: 'hibernate-validator', version: '8.0.3.Final'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.25.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.25.3'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: versions.tomcat
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: versions.tomcat

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.1.1'

  implementation group: 'com.auth0', name: 'java-jwt', version: '4.5.0'

  implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-paranamer', version: '2.18.2'

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '8.5.13'

  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.7'

  implementation group: 'io.hypersistence', name: 'hypersistence-utils-hibernate-63', version: '3.8.3'

  implementation group: 'org.awaitility', name: 'awaitility', version: '4.2.2'

  implementation 'com.microsoft.azure:applicationinsights-core:2.6.4'

  //cache
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'

  testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers
  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
  testImplementation group: 'com.h2database', name: 'h2', version: '2.3.232'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.18.0'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.18.0'


  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

  testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: versions.junitPlatform
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation(group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '3.0.1') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
    exclude group: 'org.eclipse.jetty', module: 'jetty-http'
    exclude group: 'org.eclipse.jetty', module: 'jetty-util'
    exclude group: 'org.eclipse.jetty', module: 'jetty-io'
    exclude group: 'org.eclipse.jetty', module: 'jetty-security'
    exclude group: 'org.eclipse.jetty.http2', module: 'http2-server'
  }
  testImplementation 'org.eclipse.jetty:jetty-server:11.0.26'
  testImplementation 'org.eclipse.jetty:jetty-http:11.0.26'
  testImplementation 'org.eclipse.jetty:jetty-util:11.0.26'
  testImplementation 'org.eclipse.jetty:jetty-io:11.0.26'
  testImplementation 'org.eclipse.jetty:jetty-security:11.0.26'
  testImplementation 'org.eclipse.jetty.http2:http2-server:11.0.26'

  testImplementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
  testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: versions.sonarPitest

  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junit

  testImplementation group: 'com.obsidiandynamics.pojotester', name: 'core', version: '0.9.0'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.9', classifier: 'all'
  testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '32.1.3-jre'
// https://mvnrepository.com/artifact/com.vladsch.flexmark/flexmark
  testCompileOnly group: 'com.vladsch.flexmark', name: 'flexmark-all', version: '0.64.8'


  //Pact contract testing
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pactVersion
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit', version: versions.pactVersion

  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pactVersion
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pactVersion
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pactVersion

  contractTestRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit)
  contractTestImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit)

  testUtilsImplementation sourceSets.main.runtimeClasspath
  testUtilsImplementation sourceSets.test.runtimeClasspath

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
}

task runAndPublishConsumerPactTests(type: Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED']
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    pactDirectory = 'build/pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    version = project.pacticipantVersion
    consumerVersion = project.pacticipantVersion
  }
}

pactPublish {
  pactPublish = project.pact.publish
}

task contract(type: Test) {
  description = "Runs the consumer Pact tests"
  group = 'Verification'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED']
}

task runProviderPactVerification(type: Test) {
  logger.lifecycle("Runs provider pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED']
  if (project.hasProperty('pact.verifier.publishResults')) {
    systemProperty 'pact.verifier.publishResults', project.property('pact.verifier.publishResults')
  }
  systemProperty 'pact.provider.version', project.pacticipantVersion
}

task fortifyScan(type: JavaExec)  {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

runProviderPactVerification.dependsOn contract

runProviderPactVerification.finalizedBy pactVerify

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

application {
  mainClass.set('uk.gov.hmcts.reform.wataskmanagementapi.Application')
}

bootJar {
  getArchiveFileName().set(provider {
    'wa-task-management-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
